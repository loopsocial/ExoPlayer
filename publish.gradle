// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {

    publishing {
        repositories {
            maven {
                name = 'mavenRepo'
                url = findProperty('mavenRepo') ?: "${buildDir}/repo"
            }
            maven {
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

                url = releaseVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }

        publications {
            release(MavenPublication) {
                groupId = 'com.firework.android.exoplayer'
                artifactId = releaseArtifactId
                version releaseVersion

                from components.release

                artifact androidSourcesJar

                pom {
                    name = releaseArtifactId
                    description = releaseDescription
                    url = 'https://firework.com'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'Firework'
                            name = 'Firework Android Team'
                            email = 'team-android@fireworkhq.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git@github.com:loopsocial/ExoPlayer.git'
                        url = 'https://github.com/loopsocial/ExoPlayer'
                    }
                }
            }
        }
    }

    signing {
        def signingKeyId = System.getenv("GPG_SIGNING_KEY_ID")
        def signingSecretKey = System.getenv("GPG_SIGNING_SECRET_KEY")
        def signingPassword = System.getenv("GPG_SIGNING_PASSWORD")
        useInMemoryPgpKeys(signingKeyId, signingSecretKey, signingPassword)

        sign publishing.publications.release
    }
}

// tasks.withType(PublishToMavenRepository) { it.dependsOn lint, test }

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
